model {

phi ~ dunif(0,1)
nu0 ~ dunif(0, 5)
nu1 ~ dunif(0, 2)
p0 ~ dunif(0,1)
sigma ~ dunif(0, 2)
psi ~ dunif(0,1)

for(t in 2:T) {
  gamma[t-1] <- nu0*exp(-nu1*N[t-1])
  ER[t-1] <- N[t-1]*gamma[t-1]
  totalAvail[t-1] <- sum(recruitable[,t-1])
  gammaPrime[t-1] <- ER[t-1]/totalAvail[t-1]
  Deaths[t-1] <- sum(died[,t])
  Recruits[t-1] <- sum(recruited[,t])
  }
for(i in 1:M) {
  z[i,1] ~ dbern(psi)
  recruitable[i,1] <- 1 - z[i,1]
  s[i,1] ~ dunif(xlim[1], xlim[2]) # static activity centers
  s[i,2] ~ dunif(ylim[1], ylim[2])
  for(j in 1:J) {
    d[i,j] <- sqrt((s[i,1] - X[j,1])^2 + (s[i,2] - X[j,2])^2)
    p[i,j] <- p0*exp(-d[i,j]^2/(2*sigma^2))
    }
  for(t in 2:T) {
    z[i,t] ~ dbern(z[i,t-1]*phi + recruitable[i,t-1]*gammaPrime[t-1])
    died[i,t] <- (z[i,t-1]==1) && (z[i,t]==0)
    recruited[i,t] <- (z[i,t-1]==0) && (z[i,t]==1)
    recruitable[i,t] <- 1 - max(z[i,1:t])
    for(j in 1:J) {
      for(k in 1:K) {
        y[i,j,k,t] ~ dbern(z[i,t]*p[i,j])
        }
      }
    }
  everAlive[i] <- sum(z[i,]) > 0
  }

for(t in 1:T) {
  N[t] <- sum(z[,t])
}

Ntot <- sum(everAlive)

}